M$ModelName = "LO_LQ_S1~";

(* 
   
FeynRules model file for S1~ = (3,1,-4/3) = S1tm43 scalar leptoquark. It is based on the Physics Reports 641 (2016) 1-68. See Eq. (7) of Phys. Rep. for notation. The mass (decay width) of S1~ = S1tm43 is denoted MS1t (WS1t). This model file is suitable for the leading order simulations only.

*)

M$Information = {Authors -> {"I. Dorsner", "A. Greljo"}, 
                 Version -> "5.0",
                 Date    -> "09.02.2018",
                 Emails  -> {"dorsner@fesb.hr"},
                 URLs    -> "http://lqnlo.hepforge.org"};

M$InteractionOrderHierarchy = {
  {QCD, 1},
  {QED, 2},
  {NP, 1} 
};

M$Parameters = {   

(* S1~ = S1tm43 leptoquark mass parameters *)

MS1t == { 
         ParameterType -> External,
         BlockName     -> LQPARAM,
         Value         -> 650.0,
         Description   -> "S1~ = S1tm43 mass"
        },

(* S1~ = S1tm43 leptoquark Yukawa couplings *)

yRR == { 
    	ParameterType    -> External,
	ComplexParameter -> False,
    	Indices          -> {Index[Generation], Index[Generation]},
	BlockName        -> YUKS1tRR,
    	Value            -> {yRR[1,1] -> 0.1, yRR[1,2] -> 0.0, yRR[1,3] -> 0.0,
                      	     yRR[2,1] -> 0.0, yRR[2,2] -> 0.0, yRR[2,3] -> 0.0,
                             yRR[3,1] -> 0.0, yRR[3,2] -> 0.0, yRR[3,3] -> 0.0},
    	TeX               -> Superscript[y,RR],
	InteractionOrder  -> {NP, 1},
    	Description       -> "S1~ leptoquark Yukawa coupling matrix"
},

(* S1~ = S1tm43 leptoquark multiplet decay widths *)

WS1t == {
   ParameterType -> Internal,
   Value         -> (MS1t*(Abs[yRR[1,1]]^2 + Abs[yRR[1,2]]^2 + Abs[yRR[1,3]]^2 
			 + Abs[yRR[2,1]]^2 + Abs[yRR[2,2]]^2 + Abs[yRR[2,3]]^2 
                         + Abs[yRR[3,1]]^2 + Abs[yRR[3,2]]^2 + Abs[yRR[3,3]]^2))/(16*Pi)
}

};

M$ClassesDescription = {

(********************************************)
(* Scalar singlet S1~ = (3,1,-4/3) = S1tm43 *)
(********************************************)

(* physical fields *)

S[200] == {
ClassName        -> S1tm43,
Mass             -> {MS1t, Internal},
Width            -> {WS1t, Internal},
SelfConjugate    -> False,
PropagatorLabel  -> "S1tm43",
PropagatorType   -> D,
PropagatorArrow  -> None,
QuantumNumbers   -> {Q -> -4/3, LeptonNumber -> 1},
Indices          -> {Index[Colour]},
ParticleName     -> "S1tm43",
AntiParticleName -> "S1tm43*",
FullName         -> "S1tm43"
}

};


(********************* The Lagrangian *********************)


(***** Kinetic term *****)


LS1tKin := Module[ {mu,c},
DC[S1tm43bar[c],mu] DC[S1tm43[c],mu] - MS1t^2 S1tm43bar[c] S1tm43[c]
];

(***** LQ-quark-lepton interactions *****)

LS1tYuk :=  Module[ {sp,i,j,cc},
 ExpandIndices[yRR[i,j] dRbar[sp,i,cc].CC[lR][sp,j] S1tm43[cc], FlavorExpand-> Generation]
];

(***** Complete S1~ = S1t Lagrangian *****)


LS1t := LS1tKin + LS1tYuk + HC[LS1tYuk];


(***********************************************************)